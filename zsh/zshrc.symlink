##
## Environment variables
##

export DOTFILES="$HOME/.dotfiles"

# Set the PATH environment variable
export PATH="$HOME/bin:$DOTFILES/bin:$HOME/.local/bin/:$PATH"

# Zplug directory
export ZPLUG_HOME="$HOME/.local/share/zplug/"

##
## Plugins
##

# Automaticall load zplug if it doesn't exist
if [[ ! -d $ZPLUG_HOME ]] ; then
    echo "Installing zplug..."
    git clone --quiet https://github.com/zplug/zplug $ZPLUG_HOME
fi

# Download z plugin
Z_HOME="$HOME/.local/share/z/"
if [[ ! -d "$Z_HOME" ]] ; then
    echo "Installing z.sh..."
    git clone --quiet https://github.com/rupa/z $Z_HOME
fi

source $ZPLUG_HOME/init.zsh

zplug "zsh-users/zsh-completions"
zplug "zsh-users/zsh-autosuggestions", defer:2
zplug "zsh-users/zsh-syntax-highlighting", defer:3

zplug "felixr/docker-zsh-completion"

zplug "agnoster/agnoster-zsh-theme", as:theme

zplug "$Z_HOME", from:local, use:z.sh

if ! zplug check --verbose ; then
    printf "Install zplug plugins? [y/N]: "
    if read -q ; then
        echo ; zplug install
    fi
fi

zplug load

##
## Load zsh files in the dotfiles repository
##
for file ($DOTFILES/zsh/*.zsh(N)); do
    source $file
done

##
## Allow for machine-specific local changes
##
[[ -f $HOME/.zshrc.local ]] && source $HOME/.zshrc.local

